import java.security.MessageDigest

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'java-gradle-plugin'
    alias(libs.plugins.jetbrains.kotlin.jvm)
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

group = "com.minthuya.gradle.plugin"
version = "1.2.9"

gradlePlugin {
    plugins {
        create("codexFeatureModulePlugin") {
            id = "codex-app-module-feature"
            implementationClass = "com.minthuya.gradle.plugin.FeatureModulePlugin"
        }
        create("codexBridgeModulePlugin") {
            id = "codex-app-module-bridge"
            implementationClass = "com.minthuya.gradle.plugin.BridgeModulePlugin"
        }
        create("codexPlatformModulePlugin") {
            id = "codex-app-module-platform"
            implementationClass = "com.minthuya.gradle.plugin.PlatformModulePlugin"
        }
    }
}

publishing {
    repositories {
        maven {
            url = uri("$rootDir/build-logic/repo")
        }
    }
}

gradle.taskGraph.whenReady { graph ->
    publishPluginMavenPublicationToMavenRepository.onlyIf {
        def libsFile = new File("$rootDir/gradle/libs.versions.toml")
        def libsChecksumFile = new File("$rootDir/gradle/libs.versions.toml.md5")
        def digest = MessageDigest.getInstance("MD5")
        libsFile.each { digest.update(it.bytes) }
        def currentChecksum = digest.digest().encodeHex().toString()
        def oldChecksum = ""
        if (!libsChecksumFile.exists()) {
            libsChecksumFile.createNewFile()
        }
        libsChecksumFile.withReader { oldChecksum = it.readLine() }
        println("Current libs.version.tomls's MD5 is $currentChecksum")
        println("Old libs.version.toml's MD5 is $oldChecksum")
        def needToPublish = oldChecksum != currentChecksum
        if (needToPublish) {
            println("----> Publish the plugin")
            libsChecksumFile.withWriter {
                it.writeLine(currentChecksum)
                it.close()
            }
        } else {
            println("----> No need to publish the plugin")
        }
        needToPublish
    }
}

dependencies {
    implementation "com.android.tools.build:gradle:8.4.1"
}